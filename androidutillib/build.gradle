apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

//upload maven to bintray
// 上传到bintray的aar文件，引用时的名称为
// publishedGroupId:artifact:libraryVersion
// 这三部分都能在下面的代码里找到

group = rootProject.publishedGroupId
version = rootProject.libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId rootProject.publishedGroupId
            artifactId rootProject.artifact

            name rootProject.libraryName
            description rootProject.libraryDescription
            url rootProject.siteUrl

            licenses {
                license {
                    name rootProject.licenseName
                    url rootProject.licenseUrl
                }
            }
            developers {
                developer {
                    id rootProject.developerId
                    name rootProject.developerName
                    email rootProject.developerEmail
                }
            }
            scm {
                connection rootProject.gitUrl
                developerConnection rootProject.gitUrl
                url rootProject.siteUrl
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")
    configurations = ['archives']
    pkg {
        // 组织名，我自己的这种情况是一定要写，插件的github主页有如下描述
        // userOrg – an optional organization name when the repo belongs to one of the user's orgs.
        // If not added will use 'BINTRAY_USER' by default
        // 我的maven仓库现在位于自己的org下，如果这里不写
        // 会报错 [message:Repo 'maven' was not found] 找了很久都不知道为啥
        // 最后跟另一个自己的成功的demo对比，才发现少了这个
        userOrg = rootProject.userOrg
        // 发布到Bintray的具体的仓库名
        repo = rootProject.bintrayRepo
        name = rootProject.bintrayName
        desc = rootProject.libraryDescription
        websiteUrl = rootProject.siteUrl
        vcsUrl = rootProject.gitUrl
        licenses = rootProject.allLicenses
        publish = true
        version {
            desc = rootProject.libraryDescription
        }
    }
}


// source 和 javadoc 是jfrog bintray 上 add to jcenter 必要的文件
// 会和pom，aar一起上传上去

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}